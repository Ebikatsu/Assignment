・C言語版プログラムのリスト
#include<stdio.h>
#include<stdlib.h>

unsigned int result[40];

int main()
{
	unsigned int top = 0;
	unsigned int kake;
	unsigned int agari;
	int i, t;
	result[0] = 1;
	for(kake = 2; kake <= 100; kake++)
	{
		agari = 0;
		for(i = 0; i <= top; i++)
		{
			t = result[i] * kake;
			t = t + agari;
			agari = t / 10000;
			result[i] = t % 10000;
		}
		if(agari > 0)
		{
			top ++;
			result[top] = agari;
		}
	}
	for(i = top; i >= 0; i--)printf("%d", result[i]);
	printf("\n");
	return EXIT_SUCCESS;
}
・完成したプログラムのリスト
		.data
result:		.word	0:40
enter:		.asciiz	"\n"
		.text
		.align	2
		.globl	main
main:
		#$t0はtop
		#$t1はkake
		#$t2はagari
		#$t3はi
		#$t4はt
		li	$t0,0
		li	$t5,1
		sw	$t5,result($zero)
		li	$t1,2
kakeloop:
		li	$t2,0
		li	$t3,0
iloop:
		#$t5はresult[i]
		sll	$t6,$t3,2
		lw	$t5,result($t6)
		mult	$t5,$t1		#result[i]とkakeの掛け算する。
		mflo	$t4
		add	$t4,$t4,$t2
		li	$t5,10000
		div	$t4,$t5		#tと10000の商と余りを求める。
		mflo	$t2
		mfhi	$t6
		sll	$t7,$t3,2
		sw	$t6,result($t7)
		addi	$t3,$t3,1
		ble	$t3,$t0,iloop	#topがi以上の間iloopを続ける。
		ble	$t2,$zero,else	#agariが0以下の場合elseにジャンプする。
		addi	$t0,$t0,1	#topに1を足し、result[top]にagariを代入する。
		sll	$t5,$t0,2
		sw	$t2,result($t5)
else:
		addi	$t1,$t1,1
		li	$t5,100
		ble	$t1,$t5,kakeloop	#kakeが100以下の場合kakeloopにジャンプする。
		move	$t3,$t0
printloop:					#結果を表示する。
		sll	$t5,$t3,2
		lw	$a0,result($t5)
		li	$v0,1
		syscall
		addi	$t3,$t3,-1
		bge	$t3,$zero,printloop
		la	$a0,enter
		li	$v0,4
		syscall
		li	$v0,10			#終了する。
		syscall
・プログラムの説明
$t0をtop、$t1をkake、$t2をagari、$t3をi、$t4をtとしてC言語をアセンブリに翻訳していった。
・実行結果、および、その実行結果の正しさの証明
実行結果は、
9332621544394415268169923885626670490715968264381621468592963895217599993229915608941463976156518286253697920827223758251185210916864000000
であった。
