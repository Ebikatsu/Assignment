課題B11
・目的
実行結果が正しく表示されるように課題B10のプログラムを改良する。
・C言語版プログラムのリスト
#include<stdio.h>
#include<stdlib.h>

unsigned int result[40];

int main()
{
	unsigned int top = 0;
	unsigned int kake;
	unsigned int agari;
	int i, t;
	result[0] = 1;
	for(kake = 2; kake <= 100; kake++)
	{
		agari = 0;
		for(i = 0; i <= top; i++)
		{
			t = result[i] * kake;
			t = t + agari;
			agari = t / 10000;
			result[i] = t % 10000;
		}
		if(agari > 0)
		{
			top ++;
			result[top] = agari;
		}
	}
	for(i = top; i >= 0; i--)printf(" %04d", result[i]);
	printf("\n");
	return EXIT_SUCCESS;
}
・完成したプログラムのリスト
		.data
result:		.word	0:40
enter:		.asciiz	"\n"
space:		.asciiz	" "
zero:		.asciiz "0"
		.text
		.align	2
		.globl	main
main:
		li	$t0,0
		li	$t5,1
		sw	$t5,result($zero)
		li	$t1,2
kakeloop:
		li	$t2,0
		li	$t3,0
iloop:
		sll	$t6,$t3,2
		lw	$t5,result($t6)
		mult	$t5,$t1
		mflo	$t4
		add	$t4,$t4,$t2
		li	$t5,10000
		div	$t4,$t5
		mflo	$t2
		mfhi	$t6
		sll	$t7,$t3,2
		sw	$t6,result($t7)
		addi	$t3,$t3,1
		ble	$t3,$t0,iloop
		ble	$t2,$zero,else
		addi	$t0,$t0,1
		sll	$t5,$t0,2
		sw	$t2,result($t5)
else:
		addi	$t1,$t1,1
		li	$t5,100
		ble	$t1,$t5,kakeloop
		move	$t3,$t0
printloop:
		la	$a0,space		#スペースを表示する。
		li	$v0,4
		syscall
		sll	$t6,$t3,2
		lw	$t5,result($t6)

		li	$t6,1000		#result[i]と1000,100,10の値を比較して適切な個数の0を表示する。
		bgt	$t5,$t6,zeroelse1	#result[i]が1000よりも小さい場合は0をひとつ表示し、そうでない場合はzeroelse1にジャンプする。
		la	$a0,zero
		li	$v0,4
		syscall
zeroelse1:
		li	$t6,100
		bgt	$t5,$t6,zeroelse2	#result[i]が100よりも小さい場合は0をひとつ表示し、そうでない場合はzeroelse2にジャンプする。
		la	$a0,zero
		li	$v0,4
		syscall
zeroelse2:
		li	$t6,10
		bgt	$t5,$t6,zeroelse3	#result[i]が10よりも小さい場合は0をひとつ表示し、そうでない場合はzeroelse3にジャンプする。
		la	$a0,zero
		li	$v0,4
		syscall
zeroelse3:
		move	$a0,$t5
		li	$v0,1
		syscall
		addi	$t3,$t3,-1
		bge	$t3,$zero,printloop
		la	$a0,enter
		li	$v0,4
		syscall
		li	$v0,10
		syscall
・プログラムの説明
課題B10のプログラムにスペースを表示する処理と、4桁分の結果が入った$t5の大きさを1000,100,10と比較することによって正しい個数の0を表示する処理を加えた。
・実行結果、および、その実行結果の正しさの証明
 0093 3262 1544 3944 1526 8169 9238 8562 6670 0490 7159 6826 4381 6214 6859 2963 8952 1759 9993 2299 1560 8941 4639 7615 6518 2862 5369 7920 8272 2375 8251 1852 1091 6864 0000 0000 0000 0000 0000 0000
・考察
課題B10のプログラムに、スペースを表示する機能と、適切な個数の0を表示する機能を追加した。この処理は、C言語では出力指定子によって実現できるが、アセンブリでは数十行分になってしまった。あらためてC言語は簡単であると感じた。
