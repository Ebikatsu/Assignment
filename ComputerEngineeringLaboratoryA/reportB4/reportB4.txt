・C言語版プログラムのリスト
#include<stdio.h>
#include<stdlib.h>

const int arraylength = 10;
int array[10] = {0,0,0,0,0,0,0,0,0,0};

int main(void)
{
	int c,i;
	do
	{
		printf(">");
		scanf("%d", &c);
		if(0 <= c && c < arraylength)array[c]++;
		printf("%d\n", c);
	}while(c >= 0);
	for(i = 0; i < arraylength; i++)printf("%d:%d\n", i, array[i]);
	return EXIT_SUCCESS;
}
・完成したプログラムのリスト
		.data
array:		.word	0, 0, 0, 0, 0, 0, 0, 0, 0, 0
prompt:		.asciiz	">"
enter:		.asciiz "\n"
colon:		.asciiz	":"
		.text
		.align	2
		.globl	main
main:
		la	$a0,prompt		#文字列promptを表示する。
		li	$v0,4
		syscall
		li	$v0,5			#整数を入力する。
		syscall
		move	$t0,$v0
		li	$t1,0			#入力された値が0から10の範囲にあるかどうか確認する。
		blt	$t0,$t1,printt0
		li	$t1,10
		bge	$t0,$t1,printt0		#配列の該当する要素に1を足す。
		la	$t1,array
		sll	$t2,$t0,2
		addu	$t1,$t1,$t2
		lw	$t2,0($t1)
		addiu	$t2,$t2,1
		sw	$t2,0($t1)
printt0:					#入力された値を表示する。
		move	$a0,$t0
		li	$v0,1
		syscall
		la	$a0,enter
		li	$v0,4
		syscall
		li	$t1,0			#入力された値が非負であればmainにジャンプする。
		bge	$t0,$t1,main
		li	$t0,0			#結果を表示する。
loop:
		move	$a0,$t0
		li	$v0,1
		syscall
		la	$a0,colon
		li	$v0,4
		syscall
		la	$t1,array
		sll	$t2,$t0,2
		addu	$t1,$t1,$t2
		lw	$t1,0($t1)
		move	$a0,$t1
		li	$v0,1
		syscall
		la	$a0,enter
		li	$v0,4
		syscall
		addiu	$t0,$t0,1
		li	$t1,10
		blt	$t0,$t1,loop
		li	$v0,10
		syscall
・プログラムの説明
まず文字列">"を表示し、整数の入力を促し、入力させた整数を$t0に代入する。その後$t0が0から9の範囲にある場合はarrayから$t0番目の要素をインクリメントする。その後$t0の値を表示して改行し、$t0が非負ならばmainにジャンプする。このループが終了したら、$t0でカウンタとするループで結果を表示する。
・実行結果、および、その実行結果の正しさの証明
実行結果の例としては
>3
3
>3
3
>7
7
>-1
-1
0:0
1:0
2:0
3:2
4:0
5:0
6:0
7:1
8:0
9:0
因って正しい結果を得られた。
